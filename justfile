# The recipes CI runs are the recipes a human types; that is the whole point of this file.

# List the recipes.
default:
    @just --list

# Format the sources.
fmt:
    cargo fmt

# Fail if the sources are not formatted.
fmt-check:
    cargo fmt --check

# Lint, warnings are errors.
lint:
    cargo clippy --all-targets -- -D warnings

# Lint the shell installer.
lint-sh:
    shellcheck -s sh install.sh

# Run the test suite.
test:
    cargo test

# Run the test suite and hold every region of `src/` to covered.
# The bell path writes to /dev/tty, so the runner needs a controlling terminal;
# `script` creates one and is available on both Linux (util-linux) and macOS.
#
# The bar is read from the exported segments rather than from
# `--fail-under-regions`, because the two do not agree. `src/` is compiled
# twice - once with `cfg(test)` for the lib's own test binary, once as the rlib
# the integration tests link - and `llvm-cov report` leaves a handful of spans
# unmerged between the two, so its summary counts regions as missed that
# `llvm-cov show` renders as covered. The segments are the view `show` renders,
# and they answer one question consistently: is there a region nothing reached?
# Full region coverage implies full line coverage, so that one bar is enough.
#
# A line that cannot be reached says so for itself, with a trailing
# `// coverage: off` and the reason it is unreachable. The marker is matched
# against the whole line, so it exempts every region on that line and not only
# the one that is uncovered today: keep it to lines that carry nothing else, or
# say in the comment what else it covers.
#
# `src/register/prompt.rs` is the one file excluded, and the exception is kept to
# one named file so it stays reviewable: it is the only module that knows there
# is a terminal, and exercising it means driving a pty, which would prove that
# `dialoguer` works rather than that we do. Everything worth asserting about a
# run's decisions lives in the modules it feeds, which hold the bar.
#
# An incremental instrumented build can reuse a codegen unit's old line info and
# report a region against lines the sources no longer have there - including a
# phantom miss beside a `// coverage: off` marker. `cargo llvm-cov clean` and
# rerun before believing one.
coverage:
    #!/usr/bin/env bash
    set -euo pipefail
    command -v jq >/dev/null || { echo "the coverage gate needs jq." >&2; exit 1; }
    ignore='src/register/prompt\.rs$'
    if [[ "{{os()}}" == "macos" ]]; then
        script -q /dev/null cargo llvm-cov --no-report
    else
        script -q /dev/null -c "cargo llvm-cov --no-report"
    fi
    cargo llvm-cov report --summary-only --ignore-filename-regex "$ignore"
    echo
    echo "The misses above are counted per compilation, not per region; the bar"
    echo "below is the merged region view. See the comment on this recipe."
    report="$(mktemp)"
    regions="$(mktemp)"
    trap 'rm -f "$report" "$regions"' EXIT
    cargo llvm-cov report --json --ignore-filename-regex "$ignore" --output-path "$report"
    # A segment carries [line, column, count, has-count, region-entry, gap].
    # One with a count of zero that is not a gap is a region nothing reached.
    # Written to a file rather than piped into the loop: a gate that cannot
    # fail is worse than no gate, and `set -e` does not reach into a process
    # substitution, so a `jq` that dies there would read as "nothing to report".
    jq -r '.data[].files[] | .filename as $file
           | (.segments // [])[]
           | select(.[3] and .[2] == 0 and (.[5] | not))
           | "\($file):\(.[0])"' "$report" | sort -u > "$regions"
    # The same argument: a report naming no file at all is a broken run, not a
    # clean one.
    files="$(jq -r '[.data[].files[].filename] | length' "$report")"
    if (( files == 0 )); then
        echo "the coverage report names no files; nothing was measured." >&2
        exit 1
    fi
    uncovered=0
    while IFS= read -r region; do
        [[ -n "$region" ]] || continue
        file="${region%:*}"
        line="${region##*:}"
        if [[ "$(sed -n "${line}p" "$file")" == *'// coverage: off'* ]]; then
            continue
        fi
        echo "uncovered region: $file:$line" >&2
        uncovered=$((uncovered + 1))
    done < "$regions"
    if (( uncovered )); then
        echo "$uncovered uncovered region(s) in $files file(s); the bar is all of them." >&2
        exit 1
    fi
    echo "Every region of src/ was reached, across $files files."

# The fast subset of CI: format, lints and tests.
check: fmt-check lint lint-sh test

# Build with the minimum supported Rust version from Cargo.toml.
msrv:
    #!/usr/bin/env bash
    set -euo pipefail
    msrv="$(sed -n 's/^rust-version = "\(.*\)"$/\1/p' Cargo.toml)"
    [[ -n "$msrv" ]] || { echo "Cargo.toml names no rust-version." >&2; exit 1; }
    rustup toolchain install "$msrv" --profile minimal --no-self-update
    rustup run "$msrv" cargo build --locked --all-targets

# Check the flake, build the package and run what came out of it.
nix-verify:
    #!/usr/bin/env bash
    set -euo pipefail
    nix flake check
    nix build .#tmux-agent-status
    # The packaging path is only proven by running what came out of it.
    ./result/bin/tmux-agent-status --version
    missing=0
    while IFS= read -r f; do
        rel="${f#share/agents/}"
        if [[ ! -e "result/share/agents/$rel" ]]; then
            echo "missing in Nix output: $rel" >&2
            missing=1
        fi
    done < <(find share/agents -type f -o -type l)
    exit "$missing"

# Build the release tarball and check it ships every agent file.
package-verify:
    #!/usr/bin/env bash
    set -euo pipefail
    cargo build --release --locked
    target="$(rustc -vV | sed -n 's|host: ||p')"
    dist="$(mktemp -d)"
    trap 'rm -rf "$dist"' EXIT
    name="tmux-agent-status-ci-${target}"
    mkdir -p "$dist/$name/share/tmux" "$dist/$name/share/agents"
    cp "${CARGO_TARGET_DIR:-target}/release/tmux-agent-status" "$dist/$name/"
    cp README.md LICENSE "$dist/$name/"
    cp share/tmux/tmux-agent-status.conf "$dist/$name/share/tmux/"
    # -L, not -R alone: share/agents/claude-code/hooks.json is a symlink
    # into plugins/, which the tarball does not carry.
    cp -RL share/agents/* "$dist/$name/share/agents/"
    tar -C "$dist" -czf "$dist/$name.tar.gz" "$name"
    find share/agents -type f -o -type l | sed 's|^share/agents/||' | sort > "$dist/expected"
    tar -tzf "$dist/$name.tar.gz" \
        | grep '/share/agents/.' \
        | sed 's|^[^/]*/share/agents/||' \
        | grep -v '/$' \
        | sort -u > "$dist/actual"
    if ! diff -u "$dist/expected" "$dist/actual"; then
        echo "release tarball agent files do not match share/agents/" >&2
        exit 1
    fi

# ci.yml's jobs per runner OS, as recipes; tests/ci_jobs.rs holds the two equal.
ci_linux_jobs := "fmt-check lint lint-sh test coverage msrv nix-verify package-verify"
ci_macos_jobs := "test nix-verify"

# Run ci.yml's macOS and Linux jobs locally, against a commit (default HEAD).
ci rev="HEAD":
    #!/usr/bin/env bash
    set -euo pipefail
    # Resolved once, so a commit made while the macOS jobs run cannot give the
    # Linux jobs another one.
    commit="$(just _ci-commit {{quote(rev)}})"
    just ci-macos "$commit"
    just ci-linux "$commit"

# Run the CI jobs this host can against a commit (default HEAD), noting skipped macOS jobs.
[macos]
ci-gentle rev="HEAD": (ci rev)

# Run the CI jobs this host can against a commit (default HEAD), noting skipped macOS jobs.
[linux]
ci-gentle rev="HEAD": (ci-linux rev)
    @echo "ci-gentle: the macOS jobs did not run; they need a Mac." >&2

# Run ci-gentle against the commit a branch push sends; the pre-push hook runs this.
pre-push:
    #!/usr/bin/env bash
    set -euo pipefail
    # Other pushes, such as a tag, need no check; prek itself skips deletions
    # and pushes that send no new commits.
    ref="${PRE_COMMIT_REMOTE_BRANCH:?pre-push runs from the prek pre-push hook}"
    commit="${PRE_COMMIT_TO_REF:?pre-push runs from the prek pre-push hook}"
    if [[ "$ref" != refs/heads/* ]] || [[ "$commit" =~ ^0+$ ]]; then
        echo "pre-push: $ref is not a branch update; skipping local CI." >&2
        exit 0
    fi
    just ci-gentle "$commit"

# Run ci.yml's macOS jobs on this Mac, against a commit (default HEAD).
ci-macos rev="HEAD":
    #!/usr/bin/env bash
    set -euo pipefail
    [[ "$(uname -s)" == Darwin ]] || { echo "ci-macos runs on macOS." >&2; exit 1; }
    commit="$(just _ci-commit {{quote(rev)}})"
    just _ci-snapshot "{{justfile_directory()}}" "$commit" "{{justfile_directory()}}/target/ci/macos" macos

# Run ci.yml's Linux jobs in a Docker container, against a commit (default HEAD).
ci-linux rev="HEAD":
    #!/usr/bin/env bash
    set -euo pipefail
    # The container runs the host's architecture, so on Apple silicon this is
    # aarch64 Linux where CI's ubuntu-latest is x86_64. It runs privileged
    # because the Nix build sandbox needs namespaces.
    root="{{justfile_directory()}}"
    commit="$(just _ci-commit {{quote(rev)}})"
    # The image, like the jobs, comes from the commit rather than the working tree.
    at_commit() { git -C "$root" show "$commit:$1"; }
    msrv="$(at_commit Cargo.toml | sed -n 's/^rust-version = "\(.*\)"$/\1/p')"
    llvm_cov="$(at_commit .github/workflows/ci.yml | sed -n 's/.*tool: cargo-llvm-cov@//p')"
    week="$(date +%G-W%V)"
    # Without a provenance attestation, which carries a build timestamp, the
    # image ID changes only when the image does.
    image="$(at_commit ci/linux.Dockerfile | docker build --quiet --pull --provenance=false \
        --tag tmux-agent-status-ci-linux --build-arg "IMAGE_WEEK=$week" \
        --build-arg "MSRV=$msrv" --build-arg "LLVM_COV_VERSION=$llvm_cov" -)"
    # The Nix store is kept per image: a fresh volume starts as a copy of the
    # image's /nix, and a rebuilt image's Nix never meets an older store.
    # Every image is rebuilt weekly, so stores of earlier weeks are dead and
    # go (unless a running build still holds one); this week's stay, as
    # checkouts whose commits build different images each need theirs.
    nix_volume="tmux-agent-status-ci-linux-nix-$week-$(printf '%s' "${image#sha256:}" | cut -c1-12)"
    docker volume ls --quiet --filter name='^tmux-agent-status-ci-linux-nix-' \
        | { grep -v -- "^tmux-agent-status-ci-linux-nix-$week-" || true; } \
        | while IFS= read -r stale; do docker volume rm "$stale" >/dev/null 2>&1 || true; done
    # Mounted at their host paths: a linked worktree's .git names its common
    # git directory by absolute path, and that may lie outside the checkout.
    mounts=(--volume "$root:$root:ro")
    common="$(git -C "$root" rev-parse --path-format=absolute --git-common-dir)"
    [[ "$common" == "$root"/* ]] || mounts+=(--volume "$common:$common:ro")
    # One snapshot per checkout, so runs from two worktrees cannot reset each
    # other's tree. The cargo home is shared whole: cargo keeps its package
    # cache locks at its root, not in registry/, and they hold across
    # containers on one volume.
    checkout="tmux-agent-status-ci-linux-src-$(printf '%s' "$root" | git hash-object --stdin | cut -c1-12)"
    # Each snapshot is labelled with its checkout, so the snapshots of removed
    # checkouts, such as deleted worktrees, go (unless a running build still
    # holds one). Created before the run, as `docker run` cannot label one.
    label=tmux-agent-status-ci-linux.checkout
    docker volume create --label "$label=$root" "$checkout" >/dev/null
    docker volume ls --filter label="$label" --format "{{{{.Name}}\t{{{{.Label \"$label\"}}" \
        | while IFS=$'\t' read -r volume path; do
            [[ -e "$path" ]] || docker volume rm "$volume" >/dev/null 2>&1 || true
        done
    # No --tty: a GitHub runner has no terminal, and colour is forced below.
    # Without a terminal, Ctrl-C reaches only the container's PID 1, which a
    # bash there ignores; tini as PID 1, told to signal the whole process
    # group, stops the jobs rather than leaving them running unseen.
    docker run --rm --privileged --init --env TINI_KILL_PROCESS_GROUP=1 "${mounts[@]}" \
        --volume "$checkout:/home/runner/ci" \
        --volume tmux-agent-status-ci-linux-cargo-home:/home/runner/cargo-home \
        --volume "$nix_volume:/nix" \
        --env CARGO_TERM_COLOR=always \
        "$image" \
        bash -c 'set -euo pipefail
            git config --global --add safe.directory "*"
            # CI installs the stable of the day, not the one the image baked.
            rustup update stable --no-self-update >/dev/null
            # Set after rustup, whose proxies stay in ~/.cargo from the image.
            # Only the cache moves: the tools stay on PATH in ~/.cargo/bin,
            # and anything cargo installs here would land off PATH.
            export CARGO_HOME=/home/runner/cargo-home
            just --justfile "$1/justfile" _ci-snapshot "$1" "$2" /home/runner/ci linux' \
        _ "$root" "$commit"

# Drop the Linux CI image and the volumes that cache its builds, for every checkout.
ci-linux-clean:
    #!/usr/bin/env bash
    set -euo pipefail
    volumes=()
    while IFS= read -r v; do volumes+=("$v"); done \
        < <(docker volume ls --quiet --filter name='^tmux-agent-status-ci-linux-')
    (( ${#volumes[@]} == 0 )) || docker volume rm "${volumes[@]}"
    docker image rm --force tmux-agent-status-ci-linux 2>/dev/null || true

# The full hash of a commit of this repo, or an error naming what is not one.
_ci-commit rev:
    @git -C "{{justfile_directory()}}" rev-parse --verify --quiet {{quote(rev + "^{commit}")}} \
        || { echo ci: {{quote(rev)}} names no commit. >&2; exit 1; }

# Run CI's jobs for `os` in a clean checkout of `commit`, kept under `dir` so
# builds stay incremental between runs. The job list, like the job recipes, is
# read from that checkout; only this plumbing is the working tree's.
_ci-snapshot repo commit dir os:
    #!/usr/bin/env bash
    set -euo pipefail
    src="{{dir}}/src"
    [[ -d "$src/.git" ]] || git init --quiet "$src"
    git -C "$src" fetch --quiet --no-tags "{{repo}}" "{{commit}}"
    git -C "$src" checkout --quiet --force --detach FETCH_HEAD
    git -C "$src" clean --quiet -ffdx
    echo "ci: $(git -C "$src" log -1 --format='%h %s') on $(uname -s)" >&2
    export CARGO_TARGET_DIR="{{dir}}/target"
    cd "$src"
    jobs="$(just --evaluate "ci_{{os}}_jobs")"
    # Coverage profiles from the previous run's commit pollute this one's.
    if [[ " $jobs " == *" coverage "* ]]; then
        cargo llvm-cov clean --workspace
    fi
    # Word-split on purpose: the list is recipe names.
    # shellcheck disable=SC2086
    just $jobs

# Validate the plugin and marketplace manifests (needs the `claude` CLI).
check-plugin:
    #!/usr/bin/env bash
    set -euo pipefail
    # Not a CI job, because it needs the `claude` CLI. The check that actually
    # rots - manifest against the agent doc - is a test, so it runs everywhere.
    if ! command -v claude >/dev/null 2>&1; then
        echo "claude CLI not found; skipping manifest validation." >&2
        echo "The agent-doc/manifest drift check runs in 'just test'." >&2
        exit 0
    fi
    claude plugin validate --strict .
    claude plugin validate --strict plugins/tmux-agent-status

# Build the release binary.
build:
    cargo build --release

# Shadow the installed binary with this checkout's release build.
link:
    #!/usr/bin/env bash
    set -euo pipefail
    bin_dir="${TMUX_AGENT_STATUS_BIN_DIR:-$HOME/.local/bin}"
    mkdir -p "$bin_dir"
    ln -sf "{{justfile_directory()}}/target/release/tmux-agent-status" "$bin_dir/tmux-agent-status"
    echo "$bin_dir/tmux-agent-status now shadows any installed tmux-agent-status." >&2
    echo "The shadow is invisible: 'tmux-agent-status --version' prints the resolved path." >&2
    echo "'just unlink' removes it." >&2

# Remove the dev shadow.
unlink:
    #!/usr/bin/env bash
    set -euo pipefail
    bin_dir="${TMUX_AGENT_STATUS_BIN_DIR:-$HOME/.local/bin}"
    rm -f "$bin_dir/tmux-agent-status"

# Build the nix package from this checkout.
nix-build:
    nix build .#tmux-agent-status

# A throwaway tmux server showing all four states, for looking at.
harness:
    #!/usr/bin/env bash
    set -euo pipefail
    cd "{{justfile_directory()}}"
    cargo build
    export PATH="$PWD/target/debug:$PATH"
    socket=tmux-agent-status-harness
    tmux -L "$socket" kill-server 2>/dev/null || true
    tmux -L "$socket" -f /dev/null new-session -d -s harness -x 200 -y 50 -n no-agent 'sleep 3000'
    format='#I:#{=/25/…:#{window_name}}#{?@agent_status, #{@agent_status},}#{?window_flags,#{window_flags}, }'
    tmux -L "$socket" set -g window-status-format "$format"
    tmux -L "$socket" set -g window-status-current-format "$format"
    tmux -L "$socket" set -g monitor-bell on
    tmux -L "$socket" set -g bell-action other
    export TMUX="$(tmux -L "$socket" display-message -p '#{socket_path}'),0,0"
    states=(working done error waiting)
    panes=()
    for state in "${states[@]}"; do
        panes+=("$(tmux -L "$socket" new-window -d -a -t 'harness:{end}' -n "$state" -P -F '#{pane_id}' 'sleep 3000')")
    done
    for i in "${!states[@]}"; do
        TMUX_PANE="${panes[$i]}" tmux-agent-status set "${states[$i]}"
    done
    # The hooks go on last: creating a window is itself a pane change, so they
    # would clear the states this harness exists to show before you saw them.
    tmux -L "$socket" source-file share/tmux/tmux-agent-status.conf
    echo "Attaching. Switch windows to watch the non-sticky states clear on focus." >&2
    echo "Kill it with: tmux -L $socket kill-server" >&2
    exec env -u TMUX tmux -L "$socket" attach -t harness
